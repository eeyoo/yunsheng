--==============================================================================
--脚本意图：圆形框阵结造型设计
--版本 1.0
--日期 2015-2-28
--作者 吴飞林
--注意 运行此脚本前，必须执行框阵建模脚本
--==============================================================================



--------------------------------------------------------------------------------
-------------------------------非对称造型---------------------------------------
--------------------------------------------------------------------------------
--==============================================================================
--同步达到指定位置
--参数：pos - 指定Z轴位置值
--==============================================================================
fn horizPos pos frame:FrameTime n:num =
(
	for i = 1 to n do
	(
		at time frame (
			LineArray[i].pos.z = pos
		)
	)
)

--==============================================================================
--随机位置分布
--参数：rmin, rmax - 随机位置下限和上限
--==============================================================================
fn randomPos rmin rmax frame:FrameTime n:num =
(
	for i = 1 to n do
	(
		at time frame (
			pos = random rmin rmax
			LineArray[i].pos.z = pos
		)
	)
)

--==============================================================================
--正向线性锥形
--参数1 a - 锥形斜率
--参数2 hor  - 中心高
--==============================================================================
fn linearForwardPos a hor frame:FrameTime n:num =
(
	for i = 1 to n do
	(
		at time frame (
			pos = a*(n-i) + hor
			LineArray[i].pos.z = pos
		)
	)
)

--==============================================================================
--反向线性线性锥形
--参数1 a - 锥形斜率
--参数2 hor  - 中心高
--==============================================================================
fn linearBackwardPos a hor frame:FrameTime n:num =
(
	for i = 1 to n do
	(
		at time frame (
			pos = a*(i-1) + hor
			LineArray[i].pos.z = pos
		)
	)
)

--==============================================================================
--正向抛物面
--参数1 a - 比例系数
--参数2 hor  - 中心高
--==============================================================================
fn parabForwardPos a hor frame:FrameTime n:num =
(
	for i = 1 to n do
	(
		at time frame (
			x = n-i
			pos = a*x*x + hor
			LineArray[i].pos.z = pos
		)
	)
)

--==============================================================================
--反向抛物面
--参数1 a - 比例系数
--参数2 hor  - 中心高
--==============================================================================
fn parabBackwardPos a hor frame:FrameTime n:num =
(
	for i = 1 to n do
	(
		at time frame (
			x = i-1
			pos = a*x*x + hor
			LineArray[i].pos.z = pos
		)
	)
)

--==============================================================================
--正向正弦面
--参数1 amp - 波动幅度
--参数2 hor - 中心高
--==============================================================================
fn sinForwardPos amp hor frame:FrameTime n:num =
(
	for i = 1 to n do
	(
		at time frame (
			x = 90*(n-i+1)/n
			pos = amp*sin(x) + hor
			LineArray[i].pos.z = pos
		)
	)
)

--==============================================================================
--反向正弦面
--参数1 amp - 波动幅度
--参数2 hor - 中心高
--==============================================================================
fn sinBackwardPos amp hor frame:FrameTime n:num =
(
	for i = 1 to n do
	(
		at time frame (
			x = 90*i/n
			pos = amp*sin(x) + hor
			LineArray[i].pos.z = pos
		)
	)
)


--------------------------------------------------------------------------------
-----------------------------上下对称造型---------------------------------------
--------------------------------------------------------------------------------
--==============================================================================
--线性对称沙漏型
--参数1 a - 锥形斜率
--参数2 hor  - 中心高
--==============================================================================
fn linSandPos a hor frame:FrameTime n:num =
(
	for i = 1 to n-1 by 2 do
	(
		at time frame (
			pos = a*(i+1)/2 + hor
			LineArray[i].pos.z = pos
		)
	)
	
	for i = 2 to n by 2 do
	(
		at time frame (
			pos = -a*(i/2) + hor
			LineArray[i].pos.z = pos
		)
	)
	
)

--==============================================================================
--线性对称闭合锥形
--参数1 a - 锥形斜率
--参数2 hor  - 中心高
--==============================================================================
fn linShellPos a hor frame:FrameTime n:num =
(
	for i = 1 to n-1 by 2 do
	(
		at time frame (
			pos = a*(n-i+1)/2 + hor
			LineArray[i].pos.z = pos
		)
	)
	
	for i = 2 to n by 2 do
	(
		at time frame (
			pos = -a*(n-i+2)/2 + hor
			LineArray[i].pos.z = pos
		)
	)
	
)

--==============================================================================
--对称抛物面沙漏形
--参数1 a - 比例系数
--参数2 hor  - 中心高
--==============================================================================
fn parabSandPos a hor frame:FrameTime n:num =
(
	for i = 1 to n-1 by 2 do
	(
		at time frame (
			x = (i+1)/2
			pos = a*x*x + hor
			LineArray[i].pos.z = pos
		)
	)
	
	for i = 2 to n by 2 do
	(
		at time frame (
			x = i/2
			pos = -a*x*x + hor
			LineArray[i].pos.z = pos
		)
	)
	
)

--==============================================================================
--对称抛物面贝壳形
--参数1 a - 比例系数
--参数2 hor  - 中心高
--==============================================================================
fn parabShellPos a hor frame:FrameTime n:num =
(
	for i = 1 to n by 2 do
	(
		at time frame (
			x = (n-i+1)/2
			pos = a*x*x + hor
			LineArray[i].pos.z = pos
		)
	)
	
	for i = 2 to n by 2 do
	(
		at time frame (
			x = (n-i+2)/2
			pos = -a*x*x + hor
			LineArray[i].pos.z = pos
		)
	)
	
)

--==============================================================================
--对称正弦沙漏型
--参数1 amp - 振幅
--参数2 hor  - 中心高
--==============================================================================
fn sinSandPos amp hor frame:FrameTime n:num =
(
	for i = 1 to n-1 by 2 do
	(
		at time frame (
			x = 15*(i+1)/2
			pos = amp*sin(x) + hor
			LineArray[i].pos.z = pos
		)
	)
	
	for i = 2 to n by 2 do
	(
		at time frame (
			x = 15*i/2
			pos = -amp*sin(x) + hor
			LineArray[i].pos.z = pos
		)
	)
	
)

--==============================================================================
--对称正弦贝壳型
--参数1 amp - 振幅
--参数2 hor  - 中心高
--==============================================================================
fn sinShellPos amp hor frame:FrameTime n:num =
(
	for i = 1 to n-1 by 2 do
	(
		at time frame (
			x = 15*(n-i+1)/2
			pos = amp*sin(x) + hor
			LineArray[i].pos.z = pos
		)
	)
	
	for i = 2 to n by 2 do
	(
		at time frame (
			x = 15*(n-i+2)/2
			pos = -amp*sin(x) + hor
			LineArray[i].pos.z = pos
		)
	)
	
)


--------------------------------------------------------------------------------
----------------------------产生延时效果----------------------------------------
--------------------------------------------------------------------------------
--==============================================================================
--造型函数：逐个到达指定平面
--参数1：pos1   - 起始位置
--参数2：pos2   - 结束位置
--参数3：dur    - 单个环运行时间
--参数4：delay  - 延时时间 
--==============================================================================
fn horizDelayPos pos1 pos2 dur delay frame:FrameTime n:num =
(
	frame1 = frame
	frame2 = frame1 + dur
	temp = frame + dur
	for i = 1 to n do
	(

		at time frame1 LineArray[i].pos.z = pos1
		at time frame2 LineArray[i].pos.z = pos2
	
		frame1 += delay
		frame2 = frame1 + dur
	)
	frame += (n-1)*delay + dur
	return frame
)

--==============================================================================
--造型函数：延时效果的锥形
--参数1：pos1   - 起始位置
--参数2：pos2   - 结束位置
--参数3：dur    - 单个环运行时间
--参数4：delay  - 延时时间 
--==============================================================================
fn linearDelayPos pos1 pos2 dur delay frame:FrameTime n:num =
(
	frame1 = frame
	frame2 = frame1 + dur
	temp = frame + dur
	h = pos2-pos1
	dh = h/(n-1)
	for i = 1 to n do
	(

		at time frame1 LineArray[i].pos.z = pos1
		at time frame2 LineArray[i].pos.z = pos2 - dh*(i-1)
	
		frame1 += delay
		frame2 = frame1 + dur
	)
	frame += (n-1)*delay + dur
	return frame
)

--==============================================================================
--造型函数：延时效果的正弦波动
--参数1：amp    - 正弦振幅
--参数2：count  - 运行周期数
--参数3：dur    - 单个运行一个周期时间
--参数4：delay  - 延时时间
--参数5：hor    - 中心高
--==============================================================================
fn sinDelayPos amp count dur delay hor frame:FrameTime n:num =
(
	temp = frame
	c = 8  --周期划分单位数
	dt = 360/c --周期划分一单位对应圆周度数
	df = dur/c --周期划分一单位对应时间
	for i = 1 to n do
	(
		--frame = temp + i*delay
		for t = 1 to (c*count+1) do
		(
			frame += df	
			at time frame ( LineArray[i].pos.z = amp*sin(dt*(t-1)) + hor )
			--frame += df	
		)
		frame = temp + i*delay
	)
	temp += dur*count + n*delay
	return temp
)

--------------------------------------------------------------------------------
------------------------------动静结合造型--------------------------------------
--------------------------------------------------------------------------------
--==============================================================================
--动静配合之奇偶平动，7,8,9,10,11,12保持之前造型不变
--参数1：pos1  - 奇数环位置
--参数1：pos2  - 偶数环位置
--==============================================================================
fn demoPos pos1 pos2 frame:FrameTime n:num =
(
	for i = 1 to n-7 by 2 do   --1 3 5
	(
		at time frame ( LineArray[i].pos.z = pos1 )
	)
	for j = 2 to n-6 by 2 do   --2 4 6
	(
		at time frame ( LineArray[j].pos.z = pos2 )
	)
)

--==============================================================================
--动静配合之奇偶上下固定高度对称分布
--参数1：pos - 为正奇上偶下，为负偶上奇下
--==============================================================================
fn demo1Pos pos frame:FrameTime n:num =
(
	for i = 1 to n-7 by 2 do   --1 3 5
	(
		at time frame ( LineArray[i].pos.z = pos )
	)
	for j = 2 to n-6 by 2 do   --2 4 6
	(
		at time frame ( LineArray[j].pos.z = -pos )
	)
)

--==============================================================================
--动静配合之奇偶上下对称线性分布,开口向外
--参数1：a - 为正奇上偶下，为负偶上奇下
--参数2：hor - 中心高
--==============================================================================
fn demo2APos a hor frame:FrameTime n:num =
(
	for i = 1 to n-7 by 2 do   --1 3 5
	(
		at time frame ( LineArray[i].pos.z = a*((i+1)/2 - 1) + hor )
	)
	for j = 2 to n-6 by 2 do   --2 4 6
	(
		at time frame ( LineArray[j].pos.z = -a*(j/2 - 1) + hor )
	)
)

--==============================================================================
--动静配合之奇偶上下对称线性分布，开口向内
--参数1：a - 为正奇上偶下，为负偶上奇下
--参数2：hor - 中心高
--==============================================================================
fn demo2BPos a hor frame:FrameTime n:num =
(
	for i = 1 to n-7 by 2 do   --1 3 5
	(
		at time frame ( LineArray[i].pos.z = a*((n-i-8)/2 - 1) + hor )
	)
	for j = 2 to n-6 by 2 do   --2 4 6
	(
		at time frame ( LineArray[j].pos.z = -a*((n-j-6)/2 - 1) + hor )
	)
)

--==============================================================================
--动静配合之奇偶随机分离
--参数1：a1,a2  - 奇数环上下限
--参数2：b1,b2  - 偶数环上下限
--==============================================================================
fn demo3Pos a1 a2 b1 b2 frame:FrameTime n:num =
(
	for i = 1 to n-7 by 2 do   --1 3 5
	(
		at time frame ( LineArray[i].pos.z = random a1 a2 )
	)
	for j = 2 to n-6 by 2 do   --2 4 6
	(
		at time frame ( LineArray[j].pos.z = random b1 b2 )
	)
)

--==============================================================================
--动静配合之哑铃体
--12 10 8 6 4 2 1 3 5 7 9 11由下至上分布
--==============================================================================
fn demo4Pos a frame:FrameTime n:num =
(
	for i = 2 to n by 2 do   --2 4 6 8 10 12
	(
		at time frame ( LineArray[i].pos.z = a*(n-i)*(n-i)/4 )
	)
	for j = 1 to n-1 by 2 do   --1 3 5 7 9 11
	(
		d = a*(n-1)*(n-1)/4
		at time frame ( LineArray[j].pos.z = -a*(n-j)*(n-j)/4 + 2*d)
	)
	
)

--==============================================================================
--动静配合之纺锤体
-- 2 4 6 8 10 12 11 9 7 5 3 1由下至上分布
--==============================================================================
fn demo5Pos a frame:FrameTime n:num =
(
	for i = 2 to n by 2 do   --2 4 6 8 10 12
	(
		at time frame ( LineArray[i].pos.z = a*(i)*(i)/4 )
	)
	for j = 1 to n-1 by 2 do   --1 3 5 7 9 11
	(
		d = a*(n)*(n)/4
		at time frame ( LineArray[j].pos.z = -a*(j+1)*(j+1)/4 + 2*d)
	)
)


--==============================================================================
--动静配合之上下静止，中间正弦跳动
--参数1：pos    - 静止环（7,8,9,10,11,12）上下限
--参数2：amp    - 跳动振幅
--参数3：count  - 跳动次数
--参数4：dur    - 跳动周期
--参数5：delay  - 延时时间
--参数6：hor    - 中心高
--==============================================================================
fn demo6Pos pos amp count dur delay hor frame:FrameTime n:num =
(
	
	df = dur/4
	at time frame (
		for i = 7 to n-1 by 2 do (LineArray[i].pos.z = pos + hor)
		for i = 8 to n   by 2 do (LineArray[i].pos.z = -pos + hor)
	)

	frame += 30  --延时1秒启动正弦跳动
	c = 8  --周期划分单位数
	dt = 360/c --周期划分一单位对应圆周度数
	df = dur/c --周期划分一单位对应时间
	temp = frame
	for i = 1 to n-6 do
	(
		for t = 1 to (c*count+1) do
		(
			at time frame ( LineArray[i].pos.z = amp*sin(dt*(t-1)) + hor)
			frame += df	
		)
		frame = temp + i*delay
	)
	
	return frame
)

FrameTime = 0

animate on
(
	
	--==============================================================================
	--起点零位
	horizPos 0

	--==============================================================================
	--逐个上升至500，耗时12秒360帧
	--==============================================================================
	pos1 = 0
	pos2 = 500   --由零位上升至500
	dur = 30    --首个环动作时间
	delay = 30  --每个环等待上一环动作完毕开始动作
	FrameTime = horizDelayPos pos1 pos2 dur delay
	
	--==============================================================================
	--逐个回到零位，耗时12秒360帧
	--==============================================================================
	pos1 = 500
	pos2 = 0   --由500回到零位
	dur = 30    --每个环动作时间
	delay = 30  --每个环等待上一环动作完毕开始动作
	FrameTime = horizDelayPos pos1 pos2 dur delay

	
	--==============================================================================
	--逐个上升形成锥形
	--==============================================================================
	pos1 = 0  --起始位置0
	pos2 = 500  
	dur = 30
	delay = 30
	FrameTime = linearDelayPos pos1 pos2 dur delay
	
	--==============================================================================
	--同步上升至500
	--==============================================================================
	FrameTime += 30
	horizPos 500
	
	--==============================================================================
	--同步运行形成上下对称锥形，奇数环在上，偶数环在下，耗时2秒
	--==============================================================================
	FrameTime += 60
	linSandPos 100 500
	
	--==============================================================================
	--同步运行形成上下对称锥形，奇数环在下，偶数环在上，耗时4秒
	--==============================================================================
	FrameTime += 120
	linSandPos -100 500
	
	--==============================================================================
	--同步运行形成上下对称闭合锥形，奇数环在下，偶数环在上，耗时4秒
	--==============================================================================
	FrameTime += 120
	linShellPos -100 500
	
	--==============================================================================
	--同步运行形成上下对称闭合锥形，奇数环在上，偶数环在下，耗时4秒
	--==============================================================================
	FrameTime += 120
	linShellPos 100 500
	
	
	--==============================================================================
	--同步运行形成上下对称抛物面，奇数环在上，偶数环在下，耗时4秒
	--==============================================================================
	FrameTime += 120
	parabSandPos 15 500
	
	--==============================================================================
	--同步运行形成上下对称抛物面，奇数环在下，偶数环在上，耗时4秒
	--==============================================================================
	FrameTime += 120
	parabSandPos -15 500
	
	--==============================================================================
	--同步运行形成上下对称抛物面，奇数环在上，偶数环在下，耗时4秒
	--==============================================================================
	FrameTime += 120
	parabShellPos 15 500
	
	--==============================================================================
	--同步运行形成上下对称抛物面，奇数环在上，偶数环在下，耗时4秒
	--==============================================================================
	FrameTime += 120
	parabShellPos -15 500
	
	--==============================================================================
	--同步运行形成上下对称正弦沙漏，奇数环在上，偶数环在下，耗时4秒
	--==============================================================================
	FrameTime += 120
	sinSandPos 500 500
	
	--==============================================================================
	--同步运行形成上下对称正弦沙漏，奇数环在下，偶数环在上，耗时4秒
	--==============================================================================
	FrameTime += 120
	sinSandPos -500 500
	
	--==============================================================================
	--同步运行形成上下对称正弦贝壳，奇数环在上，偶数环在下，耗时4秒
	--==============================================================================
	FrameTime += 120
	sinShellPos 500 500
	
	--==============================================================================
	--同步运行形成上下对称正弦贝壳型，奇数环在下，偶数环在上，耗时4秒
	--==============================================================================
	FrameTime += 120
	sinShellPos -500 500
	
	--==============================================================================
	--同步上升至1000，耗时2秒
	--==============================================================================
	FrameTime += 60
	horizPos 1000
	
	--==============================================================================
	--同步运行形成正向抛物面锥形，耗时4秒，运行范围0-600
	--==============================================================================
	FrameTime += 120
	parabForwardPos 5 1000
	
	--==============================================================================
	--同步运行形成正向线性锥形，耗时4秒，运行范围0-550
	--==============================================================================
	FrameTime += 120
	linearForwardPos 50 1000
	
	--==============================================================================
	--同步运行形成正向正弦面帽子，耗时4秒，总时间运行范围0-550
	--==============================================================================
	FrameTime += 120
	sinForwardPos 550 1000
	
	--==============================================================================
	--同步运行形成正反向正弦面漏斗状，耗时8秒，总时间运行范围0-550
	--==============================================================================
	FrameTime += 240
	sinBackwardPos 550 1000
	
	--==============================================================================
	--同步运行形成反向线性锥形，耗时4秒，运行范围0-550
	--==============================================================================
	FrameTime += 120
	linearBackwardPos 50 1000
	
	--==============================================================================
	--同步运行形成反向抛物面锥形，耗时4秒，运行范围0-600
	--==============================================================================
	FrameTime += 120
	parabBackwardPos 5 1000
	
	--==============================================================================
	--同步上升至1500，耗时2秒
	--==============================================================================
	FrameTime += 60
	horizPos 1500
	
	--==============================================================================
	--1500高度停止0.5秒
	--==============================================================================
	FrameTime += 15
	horizPos 1500

	--==============================================================================
	--逐个启动正弦波动，运行两周期，周期时间8秒，总时间8秒，运行绝对范围500-1500
	--==============================================================================
	amp = 500
	count = 2
	dur = 240
	delay = 15  --延时0.5秒
	hor = 1000  --中心高不变
	FrameTime = sinDelayPos amp count dur delay hor
	
	--==============================================================================
	--再次逐个启动正弦波动，运行两周期，周期时间8秒，总时间8秒，运行绝对范围0-1000
	--==============================================================================
	amp = 500
	count = 2
	dur = 240
	delay = 15  --延时0.5秒
	hor = 500  --中心高不变
	FrameTime = sinDelayPos amp count dur delay hor
	
	--==============================================================================
	--500高度停止0.5秒
	--==============================================================================
	FrameTime += 15
	horizPos 500
	
	--------------------------------------------------------------------------------
	------------------------------动静结合造型--------------------------------------
	--------------------------------------------------------------------------------
	--==============================================================================
	--动静配合之奇偶平动，7,8,9,10,11,12保持之前造型不变，耗时4秒
	--==============================================================================
	FrameTime += 120
	pos1 = 1000 --奇数环上升至1000
	pos2 = 800 --偶数环上升至800
	demoPos pos1 pos2
	
	--==============================================================================
	--动静配合之奇偶平动，7,8,9,10,11,12保持之前造型不变，耗时1秒
	--==============================================================================
	FrameTime += 30
	pos1 = 800 --奇数环下降至800
	pos2 = 1000 --偶数环上升至600
	demoPos pos1 pos2
	
	
	--==============================================================================
	--动静配合之奇偶平动，7,8,9,10,11,12保持之前造型不变，耗时1秒
	--==============================================================================
	FrameTime += 30
	pos1 = 900 --奇数环上升至900
	pos2 = 900 --偶数环下降至900
	demoPos pos1 pos2
	
	--==============================================================================
	--动静配合之奇偶上下对称线性分布,开口向外
	--参数1：a - 为正奇上偶下，为负偶上奇下
	--参数2：hor - 中心高
	--==============================================================================
	FrameTime += 60
	demo2APos 100 900
	
	
	--==============================================================================
	--动静配合之奇偶上下对称线性分布,开口向外
	--参数1：a - 为正奇上偶下，为负偶上奇下
	--参数2：hor - 中心高
	--==============================================================================
	FrameTime += 120
	demo2BPos 100 900

	

	FrameTime
	
)